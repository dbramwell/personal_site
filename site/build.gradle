plugins {
    id "com.moowork.node" version "0.10"
}

apply plugin: 'groovy'

ext.junitVersion = '4.11'
ext.seleniumVersion = '2.46.0'

sourceSets {
    integrationTest {
        groovy {
            srcDir "src/integrationTest/groovy"
        }
    }
}

dependencies {
    integrationTestCompile 'org.codehaus.groovy:groovy-all:2.4.3',
    "junit:junit:${junitVersion}",
    "org.seleniumhq.selenium:selenium-firefox-driver:${seleniumVersion}",
    "org.seleniumhq.selenium:selenium-chrome-driver:${seleniumVersion}",
    "org.seleniumhq.selenium:selenium-api:${seleniumVersion}",
    "org.seleniumhq.selenium:selenium-java:${seleniumVersion}"
}

node {
    version = '0.12.5'
    npmVersion = '2.1.5'
    distBaseUrl = 'http://nodejs.org/dist'
    download = true
    workDir = file("${project.buildDir}/nodejs")
    nodeModulesDir = file("${project.projectDir}/src/app")
}

def stopLocal() {
    logger.quiet("Killing node...")
    def killProcess = "killall node".execute()
    killProcess.in.eachLine {line ->
        logger.quiet line
    }

    logger.quiet killProcess.err.text
}

npmInstall {
    execOverrides {
        it.workingDir = "${project.projectDir}/src/app"
    }
}

task localDeploy(dependsOn: npmInstall) {
    doFirst {
        stopLocal()
        logger.quiet("Node instances killed, starting app...")
    }
    doLast {
        def startProcess = "${project.node.workDir}/node-v${project.node.version}-linux-x64/bin/node $project.projectDir/src/app/personal_site.js"
                .execute()
    }
}

task integrationTest(type: Test, dependsOn: npmInstall) {
    doFirst { localDeploy.execute() }
    inputs.dir "$project.projectDir/src/app"
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    doLast {
        stopLocal()
    }
}

task deployToHeroku(type: Exec, dependsOn: integrationTest) {
    workingDir "${project.rootDir}"
    commandLine "${project.projectDir}/src/scripts/deploy_to_heroku.sh"
    args = ["$project.rootDir"]
}

